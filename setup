#!/usr/bin/env perl
package main;

use 5.010;
use strict;
use warnings;
use Getopt::Long;
use Pod::Usage;
import util;

our $VERSION    = 0.1.0;

my %config      = (
    "domain"        => undef,
);

{   # Parse options.
    Getopt::Long::Configure qw(gnu_getopt);
    my $optVerbose;
    my $optQuiet;
    my $optDryrun;
    my %optConfig;

    GetOptions(
        'verbose|v'     =>  \$optVerbose,
        'quiet|q'       =>  \$optQuiet,
        'dryrun|n'      =>  \$optDryrun,
        'config|c=s'    =>  \%optConfig,
    ) || exit(1);
    $util::verbose  = $optVerbose ? 1 : ($optQuiet ? 3 : 2 );
    $util::dryrun   = $optDryrun;

    # parse config
    while (my ($key,$value) = each %optConfig) {
        if(exists $config{$key}){
            $config{$key} = $value;
        }else{
            pod2usage(
                -msg => "Invalid config key '$key' specified. Supported keys are:",
                -sections => "CONFIGKEY",
                -verbose => 99);
        }
    }
}

my $optType     = shift || "help";

my %flow = (
    install   => sub {
        unless ($config{'domain'}){
            pod2usage(
                -msg        => "domain not set",
                -sections   => "DESCRIPTION",
                -verbose    => 99);
        }

    my $pdnsConf    = << "EOM";
lauch+=pipe
pipe-command=/usr/local/bin/xip-pdns /etc/powerdns/xip-pdns.conf
EOM

    my $xipConf     = << "EOM";
XIP_DOMAIN="$config{'domain'}"
EOM

        my $s1 = [];
        regSteps($s1, "Install required packages.", sub {
            installPackageSuite("git")
            || installPackageSuite("pdns-pipe");
        });
        regSteps($s1, "Copy bin.", sub {
            runCmd('git clone https://github.com/basecamp/xip-pdns.git')
            || runCmd('cp xip-pdns/bin/xip-pdns /usr/local/bin/');
        });
        regSteps($s1, "Install config file.", sub {
            installFile('/etc/xip-pdns.conf', $xipConf)
            || installFile('/etc/powerdns/pdns.d/pdns.pipe.conf',$pdnsConf);
        });
        return $s1;
    },
);

my $getSteps = $flow{$optType};
pod2usage(1) unless $getSteps;
runSteps(&$getSteps);

BEGIN{
package util;

use 5.010;
use strict;
use warnings;
use Exporter qw(import);

our @EXPORT        = qw(installFile installPackageSuite runCmd runSteps regSteps enableService
    LOGDEBUG LOGINFO LOGWARN LOGERR info);
our %EXPORT_TAGS    = (logging => [qw(LOGDEBUG LOGINFO LOGWARN LOGERR info)]);

my $rpmRepo = "http://doliutm.trafficmanager.net/zabbix/2.2/rhel/7/x86_64/zabbix-release-2.2-1.el7.noarch.rpm";

use constant {
    LOGDEBUG  => 1,
    LOGINFO   => 2,
    LOGWARN   => 3,
    LOGERR    => 4,
};

our $verbose        = LOGINFO;
our $dryrun         = 1;
our $defaultRetry   = 2;
our $defaultSleep   = 5;

sub installFile
{
    my $path        = shift;
    my $content     = shift;
    my $FL;

    return 0 if $dryrun;

    open($FL, '>', $path);
    print $FL $content;
    close $FL;
    return 0;
}

sub setLogLevel
{
    $verbose = shift;
}

sub info
{
    my $msg     = shift;
    my $level   = shift || LOGINFO;

    return if ($verbose > $level);
    say "[MONICAKE] $msg";
}

sub runCmd
{
    my $cmd = shift;
    if ($verbose >= LOGINFO) { $cmd .= " >/dev/null"; }

    info "Run command:\n$cmd", LOGDEBUG;
    return 0 if $dryrun;

    my $ret = system($cmd);
    if ($ret){
        info "Return code is $ret", LOGDEBUG;
    }

    return $ret;
}


my %suites = (
    'git'               => ['git'],
    'pdns-pipe'         => ['pdns-backend-pipe'],
);

sub installPackageSuite
{
    my $packageSuite    = shift;
    my $packageManager  = getPackageManager();
    return 1 unless $packageManager;

    my $packageInstall  = $$packageManager{install};

    unless(exists $suites{$packageSuite}){
        info("Package suite $packageSuite not supported.");
        exit(4);
    }

    my $packagesRef = $suites{$packageSuite};
    my $packageLine = join(" ", @$packagesRef);
    my $cmd = "$packageInstall $packageLine";
    info "Begin install packages:$packageLine", LOGDEBUG;
    my $ret = runCmd($cmd);
    if (!$ret){
        info("Install succeed.", LOGDEBUG);
    }else{
        info("Install failed. Try update.", LOGDEBUG);
        my $packageUpdate = $$packageManager{refresh};
        runCmd($packageUpdate);
        $ret = runCmd($cmd);
        if (!$ret){
            info("Install succeed.", LOGDEBUG);
        }else{
            info("Install failed.", LOGDEBUG);
        }
    }

    return $ret;
}

sub getPackageManager()
{
    my %pm  = (
        aptitude    => {
            install => "DEBIAN_FRONTEND=noninteractive apt-get -y --no-install-recommends install",
            refresh => "apt-get update"
        },
        yum         => {
            install => "yum -y install",
            refresh => "rpm -q zabbix-release || rpm -ivh $rpmRepo"
        }
    );

    return $pm{aptitude}    unless runCmd('which apt-get 2> /dev/null');
    return $pm{yum}         unless runCmd('which yum 2> /dev/null');

    info("Package manager not supported.", LOGERR);
    exit(3);
}

sub regSteps
{
    my $steps       = shift;
    my $name        = shift;
    my $sub         = shift;
    push @$steps, {
        name    => $name,
        run     => $sub,
    };
}

sub runSteps
{
    my $steps   = shift;
    my $len     = @$steps;
    my $index   = 0;
    foreach my $step (@$steps){
        ++$index;
        info "($index/$len) $$step{name}";
        my $run = $$step{run};
        my $ret = &$run;
        my $retry = $defaultRetry;
        while ($ret && $retry){
            info "failed with code: $ret, $retry retries remaining. Sleep for $defaultSleep seconds...";
            sleep($defaultSleep);
            --$retry;
            $ret = &$run;
        }

        if ($ret){
            info "failed with code: $ret";
            exit(2);
        }
    }
}

sub enableService
{
    my $serviceName     = shift;
    my $serviceManager  = getServiceManager();
    return 1 unless $serviceManager;
    my $cmd = sprintf $$serviceManager{enable}, $serviceName;
    my $ret = runCmd($cmd);
    
    return $ret;
}

sub getServiceManager
{
     my %pm  = (
        systemctl    => {
            enable => "systemctl enable %s",
        },
        upstart      => {
            enable => "echo %s",
        }
    );

    return $pm{systemctl}       unless runCmd('which systemctl 2> /dev/null');
    return $pm{upstart}         unless runCmd('which status 2> /dev/null');

    info("Service manager not supported.", LOGERR);
    exit(3);
}

1;
}

__END__
=head1 NAME

setup - Install xip service

=head1 SYNOPSIS

run [-nqv] install [-c configKey=configValue]

=head1 DESCRIPTION

B<This program> will help setup xip service.

=head1 OPTIONS

=over 4

=item B<-n>

Dry run.

=item B<-q>

Show less information.

=item B<-v>

Be more verbose.

=item B<-c>

Key configruation.

=back

=head1 CONFIGKEY

=head2 install

=over 4

=item B<domain>

Required. Top level domain

=back

=cut
